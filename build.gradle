plugins {
    id "java"
    id 'jigsaw-cosmic-reach'
    id 'maven-publish'
}

def ref = System.getenv("GITHUB_REF")

group = p_group
version = ((ref == null) ? "refs/tags/0.0.0-alpha" : ref).replaceAll("refs/tags/", "")

String mavenUrl = System.getenv("MAVEN_URL")

String envRepo = System.getenv("MAVEN_REPO");
String mavenRepo = envRepo != null ? envRepo : "releases";

jigsawGame {
    splitSourceSets()
}

sourceSets {
    common {
        java {
            srcDirs "src/common/java"
            srcDirs "api/src/common/java"
        }
        resources {
            srcDirs "src/common/resources"
            srcDirs "api/src/common/resources"
        }
    }
	//client {
	//    java {
    //        srcDirs "src/client/java"
    //        srcDirs "api/src/client/java"
    //    }
    //    resources {
    //        srcDirs "src/client/resources"
    //        srcDirs "api/src/client/resources"
    //    }
	//}

}

jigsawAccess {
    addManipulators(file("$projectDir/src/common/resources/cosmic-api.manipulator"))
}

jigsawInject {
    addInterfaceInjectors(file("$projectDir/src/common/resources/cosmic-api-injector.inject"))
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        name = 'Puzzle'
        url = 'https://maven.puzzleshq.dev/releases/'
    }
}

jar {
    duplicatesStrategy = 'EXCLUDE'
}

dependencies {
    clientCompileOnly "org.jetbrains:annotations:$jetbrains_annotations_version"
    commonCompileOnly "org.jetbrains:annotations:$jetbrains_annotations_version"

    puzzleLoader("dev.puzzleshq:puzzle-loader-core:${puzzle_core_version}")
    puzzleLoader("dev.puzzleshq:puzzle-loader-cosmic:${puzzle_cosmic_version}")
    cosmicReach("finalforeach:cosmic-reach:${cosmic_reach_version}")
}

processCommonResources {
    duplicatesStrategy = 'EXCLUDE'

    def projectProperties = [
            "p_name": p_name,
            "p_version": ((String)version).contains(".") ? version : "0.0.0",
            "p_desc": p_desc,

            "p_id": p_id,
            "p_group": p_group,
    ]

    inputs.properties projectProperties

    filesMatching(["puzzle.mod.json"]) {
        expand projectProperties
    }
}

tasks.register("buildMergedSourcesJar", Jar) {
    duplicatesStrategy = 'EXCLUDE'
    from sourceSets.client.allSource
    from sourceSets.common.allSource
    from sourceSets.server.allSource
    archiveClassifier = "sources"
}

publishing {
    repositories {
        maven {
            name = "PuzzleHQsMaven"
            url = mavenUrl+ "/" +mavenRepo
            credentials {
                username = System.getenv("MAVEN_NAME")
                password = System.getenv("MAVEN_SECRET")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = p_group
            artifactId = p_id

            from components.java

            artifact source: buildMergedJar, extension: 'jar'
            artifact source: buildMergedSourcesJar, extension: 'jar', classifier: "sources"

            pom {
                name = p_name
                description = p_desc
                url = p_git

                licenses {
                    license {
                        name = "GNU Lesser General Public License"
                        url = "https://www.gnu.org/licenses/lgpl-3.0.html"
                        distribution = "LGPL"
                        comments = "This project includes LGPL-licensed components."
                    }
                }

                developers {
                    developer {
                        id = 'mrzombii'
                        name = 'Mr Zombii'
                        email = 'thezombiigames@gmail.com'
                    }
                    developer {
                        id = 'crabking'
                        name = 'CrabKing'
                    }
                    developer {
                        id = 'repletsin5'
                        name = 'repletsin5'
                    }
                }
                scm {
                    connection = "${p_git}.git"
                    developerConnection = "${p_git}.git"
                    url = "${p_git}.git"
                }
            }
        }
    }
}

if (ref != null) {
    signing {
        useGpgCmd()
        sign(publishing.publications[0])
    }
}
